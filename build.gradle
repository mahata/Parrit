plugins {
    id "java"
    id "org.springframework.boot" version "2.7.18"
    id "io.spring.dependency-management" version "1.0.15.RELEASE"
    id "org.flywaydb.flyway" version "6.3.1"
}

repositories {
    mavenCentral()
}

bootJar {
    archiveFileName = "parrit-app.jar"
}

dependencies {
    ext.commonsLang3Version = "3.9"

    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.boot:spring-boot-starter-thymeleaf"
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-devtools"
    implementation "org.apache.commons:commons-lang3:$commonsLang3Version"
    implementation 'org.flywaydb:flyway-core'

    runtimeOnly "org.postgresql:postgresql"

    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    testCompileOnly 'org.projectlombok:lombok:1.18.36'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.36'

    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.security:spring-security-test"

    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    testImplementation "org.testcontainers:testcontainers:1.19.3"
    testImplementation "org.testcontainers:junit-jupiter:1.19.3"
    testImplementation "org.testcontainers:postgresql:1.19.3"
}

test {
    useJUnitPlatform() {
        includeEngines 'junit-jupiter'
        excludeEngines 'junit-vintage'
    }
}

version "1.1.0"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

// *** Database Tasks *** //

flyway {
    url = 'jdbc:postgresql://localhost:5432/local_parrit'
    user = 'parrit'
    password = 'parrit'
}

task migrate(type: org.flywaydb.gradle.task.FlywayMigrateTask) {
}

// *** LifeCycle Tasks *** //

defaultTasks "clean", "build"

task jsDependencies(type: Exec) {
    group = "Parrit"
    description = "Downloads dependencies for the frontend javascript code"

    workingDir = "$project.projectDir/frontend"
    commandLine = "npm"
    args = ["install"]
}

task jsLint(type: Exec) {
    group = "Parrit"
    description = "Checks the javascript code syntax"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/.bin/eslint"
    args = ["--config", ".eslintrc.json", "."]
    environment "NODE_OPTIONS", "--openssl-legacy-provider"
}
jsLint.dependsOn(jsDependencies)

task jsTest(type: Exec) {
    group = "Parrit"
    description = "Runs the frontend javascript tests"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/.bin/jest"
    environment "NODE_OPTIONS", "--openssl-legacy-provider"
}
jsTest.dependsOn(jsLint)
test.dependsOn(jsTest)

task jsCompile(type: Exec) {
    group = "Parrit"
    description = "Compiles the frontend typescript"

    workingDir = "$project.projectDir/frontend"
    commandLine = "npm"
    args = ["run", "build"]
    environment "NODE_OPTIONS", "--openssl-legacy-provider"
}
jsCompile.dependsOn(jsDependencies)

task jsBuild(type: Exec) {
    group = "Parrit"
    description = "Bundles the frontend javascript code"

    workingDir = "$project.projectDir/frontend"
    commandLine = "./node_modules/.bin/webpack"
    args = ["--config", "webpack.config.js"]
    environment "NODE_OPTIONS", "--openssl-legacy-provider"
}
jsBuild.dependsOn(jsCompile)
processResources.dependsOn(jsBuild)


